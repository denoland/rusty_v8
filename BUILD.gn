# Copyright 2018-2019 the Deno authors. All rights reserved. MIT license.

static_library("rusty_v8") {
  complete_static_lib = true
  sources = [
    "src/binding.cc",
    "v8/src/wasm/c-api.cc",
    "v8/src/wasm/c-api.h",
    "v8/third_party/wasm-api/wasm.h",
    "v8/third_party/wasm-api/wasm.hh",
  ]
  deps = [
    "//build/config:shared_library_deps",
    "//v8:v8",
    "//v8:v8_libbase",
    "//v8:v8_libplatform",
    "//v8:v8_libsampler",
  ]
  configs -= [
    "//build/config/compiler:default_init_stack_vars",
    "//build/config/compiler:thin_archive",
  ]
  configs += [ ":rusty_v8_config" ]
}

config("rusty_v8_config") {
  configs = [ "//v8:external_config" ]
  cflags = []

  # We need these directories in the search path to be able to include some
  # internal V8 headers.
  include_dirs = [
    "v8",
    "$target_gen_dir/v8",
  ]

  if (is_debug) {
    defines = [ "DEBUG" ]
  }

  if (is_clang) {
    cflags += [
      "-fcolor-diagnostics",
      "-fansi-escape-codes",
    ]
  }

  if (is_debug && is_clang && !is_win) {
    cflags += [ "-glldb" ]
  }

  if (is_win) {
    # The `/Zl` ("omit default library name") flag makes the compiler produce
    # object files that can link with both the static and dynamic CRT.
    cflags += [ "/Zl" ]
  }
}

# Copyright 2018-2019 the Deno authors. All rights reserved. MIT license.
import("//build/config/host_byteorder.gni")

static_library("rusty_v8") {
  complete_static_lib = true
  sources = [ "src/binding.cc" ]
  deps = [
    "//build/config:shared_library_deps",
    "//v8:v8",
    "//v8:v8_libbase",
    "//v8:v8_libplatform",
  ]
  configs -= [
    "//build/config/compiler:default_init_stack_vars",
    "//build/config/compiler:thin_archive",
  ]
  configs += [ ":rusty_v8_config" ]
}

config("rusty_v8_config") {
  configs = [
    "//v8:external_config",
    "//v8:toolchain",
    "//v8:features",
  ]
  cflags = []

  # We need these directories in the search path to be able to include some
  # internal V8 headers.
  include_dirs = [
    "v8",
    "$target_gen_dir/v8",
  ]

  if (is_debug) {
    defines = [ "DEBUG" ]
  }

  if (is_clang) {
    cflags += [
      "-fcolor-diagnostics",
      "-fansi-escape-codes",
    ]
  }

  if (is_debug && is_clang && !is_win) {
    cflags += [ "-glldb" ]
  }
}
